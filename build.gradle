apply plugin: 'java'

task wrapper(type: Wrapper) {
	gradleVersion = '2.2'
}

ext {
	stardogVersion = project.hasProperty("stardogVersion") ? project.getProperty("stardogVersion") : '7.0.2'
	projectVersion = '0.0.6'

	distPath = "${projectDir}/dist"
	stardogRepo = project.hasProperty("stardogRepo") ? project.getProperty("stardogRepo") : "http://maven.stardog.com"
	stardogLib = project.hasProperty("stardogLib") ? project.getProperty("stardogLib") : "."
}

allprojects {
	apply plugin: 'idea'
	group = 'com.complexible.stardog'
	version = projectVersion
	// also configure in idea.gradle
	sourceCompatibility = '1.8'
	targetCompatibility = '1.8'

	repositories {
		maven {
			url stardogRepo
		}
		mavenLocal()
		mavenCentral()
	}

	tasks.withType(Test) {
		systemProperty 'stardog.home', rootProject.projectDir
		systemProperty 'java.library.path', project.stardogLib
	}
}

subprojects {
	apply plugin: "java"

	// common deps
	dependencies {
		compile 'org.slf4j:slf4j-api:1.7.7'
		compile 'org.slf4j:slf4j-jdk14:1.7.7'

		testCompile 'junit:junit:4.8.2'
	}

	sourceSets {
		main {
			java {
				srcDir 'main/src'
			}
			resources {
				srcDir 'main/resources'
			}
		}
		test {
			java {
				srcDir 'test/src'
			}
			resources {
				srcDir 'test/resources'
			}
		}
	}
}
