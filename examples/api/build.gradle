group = "com.complexible.stardog.examples.api"

dependencies {
	// server core modules
	compile "com.complexible.stardog:server:${stardogVersion}@pom"
	compile "com.complexible.stardog.cache:stardog-cache-core:${stardogVersion}"
	compile "com.stardog.stark.query:stardog-stark-query-api:${stardogVersion}"
	compile "com.stardog.stark.io:stardog-stark-io-api:${stardogVersion}"
	compile "com.stardog.stark.query.io:stardog-stark-query-io:${stardogVersion}"

	// client modules
	compile "com.complexible.stardog:client-embedded:${stardogVersion}"
	compile "com.complexible.stardog:client-http:${stardogVersion}@pom"

	// archetypes
	runtime "com.complexible.stardog.prov:stardog-prov:${stardogVersion}"
	runtime "com.complexible.stardog.skos:stardog-skos:${stardogVersion}"

	// external apis
	compile "com.complexible.stardog.rdf4j:stardog-rdf4j:${stardogVersion}"
	compile "org.eclipse.rdf4j:rdf4j-runtime:2.2.4"
	compile "com.complexible.stardog.jena:stardog-jena:${stardogVersion}"
}

task execute(type:JavaExec, dependsOn: compileJava) {

	if (project.hasProperty("mainClass")) {
		main = project.mainClass
	}
	else {
		main = "com.complexible.stardog.examples.api.ConnectionAPIExample"
	}

	classpath = sourceSets.main.runtimeClasspath

	jvmArgs = [ "-Djava.library.path=${project.stardogLib}" ]

	if (project.hasProperty("stardogHome")) {
		jvmArgs += "-Dstardog.home=${project.stardogHome}"
	}
}

task executeAll(dependsOn: compileJava) doLast {
	sourceSets.main.allJava.each { File aFile ->
		if (aFile.getName().contains("Example")) {
			runExample aFile.getAbsolutePath()
		}
	}
}

task docs doLast {
	exec {
		commandLine "docco",
					"-o",
					"${distPath}/docs",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionPoolsExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ExplanationExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/GraphQLExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ReasoningExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/SHACLExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/WaldoAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/jena/JenaExample.java",
					"${srcPath}/com/complexible/stardog/examples/rdf4j/RDF4JExample.java"
	}
}

def runExample(String example) {
	// replace / with .
	String fqcn = example.replaceAll(System.getProperty("file.separator"), ".");
	// strip .java
	fqcn = fqcn.substring(0, fqcn.length() - 5);
	fqcn = fqcn.substring(fqcn.indexOf("com.complexible"));

	javaexec {

		main = fqcn

		classpath = project(':examples:api').sourceSets.main.runtimeClasspath


		jvmArgs = [ "-Djava.library.path=${project.stardogLib}" ]

		if (project.hasProperty("stardogHome")) {
			jvmArgs += "-Dstardog.home=${project.stardogHome}"
		}
		// else stardog will attempt to use its own $STARDOG_HOME
	}
}
