group = "com.complexible.stardog.examples.api"

dependencies {
	// server core modules
	implementation platform("com.complexible.stardog:server:${stardogVersion}")
	implementation "com.complexible.stardog.protocols.http:stardog-protocols-http-server:${stardogVersion}"
	implementation "com.complexible.stardog.cache:stardog-cache-core:${stardogVersion}"
	implementation "com.complexible.stardog.graphql.http:stardog-graphql-protocols-http-server:${stardogVersion}"
	implementation "com.complexible.stardog.icv.http:stardog-icv-protocols-http-server:${stardogVersion}"
	implementation "com.complexible.stardog.reasoning.http:stardog-reasoning-protocols-http-server:${stardogVersion}"

	// client modules
	implementation platform("com.complexible.stardog:client-http:${stardogVersion}")
	implementation "com.complexible.stardog.graphql.http:stardog-graphql-protocols-http-client:${stardogVersion}"
	implementation "com.complexible.stardog.icv.http:stardog-icv-protocols-http-client:${stardogVersion}"
	implementation "com.complexible.stardog.reasoning.http:stardog-reasoning-protocols-http-client:${stardogVersion}"
	implementation "com.complexible.stardog.search:stardog-search-shared:${stardogVersion}"
	implementation "com.complexible.stardog.search.http:stardog-search-protocols-http-client:${stardogVersion}"

	// for serializing query results and rdf
	implementation "com.stardog.stark.io:stardog-stark-io-api:${stardogVersion}"
	implementation "com.stardog.stark.query.io:stardog-stark-query-io:${stardogVersion}"

	// external apis
	implementation "com.complexible.stardog.rdf4j:stardog-rdf4j-core:${stardogVersion}"
	implementation "org.eclipse.rdf4j:rdf4j-runtime:2.2.4"
	implementation "com.complexible.stardog.jena:stardog-jena:${stardogVersion}"
}

task execute(type:JavaExec, dependsOn: compileJava) {

	if (project.hasProperty("mainClass")) {
		main = project.mainClass
	}
	else {
		main = "com.complexible.stardog.examples.api.ConnectionAPIExample"
	}

	classpath = sourceSets.main.runtimeClasspath

	jvmArgs = [ "-Djava.library.path=${project.stardogLib}" ]

	if (project.hasProperty("stardogHome")) {
		jvmArgs += "-Dstardog.home=${project.stardogHome}"
	}
}

task executeAll(dependsOn: compileJava) doLast {
	sourceSets.main.allJava.each { File aFile ->
		if (aFile.getName().contains("Example")) {
			runExample aFile.getAbsolutePath()
		}
	}
}

task docs doLast {
	exec {
		commandLine "docco",
					"-o",
					"${distPath}/docs",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionAPIExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ConnectionPoolsExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/GraphQLExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ReasoningExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/ReasoningExplanationExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/SHACLExample.java",
					"${srcPath}/com/complexible/stardog/examples/api/SearchExample.java",
					"${srcPath}/com/complexible/stardog/examples/jena/JenaExample.java",
					"${srcPath}/com/complexible/stardog/examples/rdf4j/RDF4JExample.java"
	}
}

def runExample(String example) {
	// replace / with .
	String fqcn = example.replaceAll(System.getProperty("file.separator"), ".");
	// strip .java
	fqcn = fqcn.substring(0, fqcn.length() - 5);
	fqcn = fqcn.substring(fqcn.indexOf("com.complexible"));

	javaexec {

		main = fqcn

		classpath = project(':examples:api').sourceSets.main.runtimeClasspath


		jvmArgs = [ "-Djava.library.path=${project.stardogLib}" ]

		if (project.hasProperty("stardogHome")) {
			jvmArgs += "-Dstardog.home=${project.stardogHome}"
		}
		// else stardog will attempt to use its own $STARDOG_HOME
	}
}
