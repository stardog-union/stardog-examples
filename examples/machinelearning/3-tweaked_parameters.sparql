prefix agg: <urn:aggregate>
prefix spa: <tag:stardog:api:analytics:>

INSERT {
    graph spa:model {
        :r2 a spa:RegressionModel ;
                spa:parameters [
                    spa:loss_function 'squared' ;
                    spa:l 0.08 ;
                    spa:b 22 ;
                    spa:l1 0.000001 ;
                    spa:passes 2 ;
                    spa:k True ;
                    spa:c True ;
                    spa:holdout_off True ;
                ] ;
                spa:arguments (?authors ?directors ?producers ?keywords ?contentRating ?year ?metaCritic) ;
                spa:predict ?rating ;
                spa:crossValidation 100 ;
                spa:validationMetric spa:mae .
    }
}
WHERE {
    SELECT         
        (agg:spa:set(?author) as ?authors)
        (agg:spa:set(?director) as ?directors)
        (agg:spa:set(?producer) as ?producers)
    	(agg:spa:set(?keyword) as ?keywords)
        ?contentRating
        ?year
        ?metaCritic
        ?rating
    {
        ?movie  :rating ?rating ;
                :author ?author ;
                :director ?director ;
                :keyword ?keyword ;
                :contentRating ?contentRating ;
                :copyrightYear ?year .

        OPTIONAL {
            ?movie  :productionCompany ?producer .
        }
        OPTIONAL {
            ?movie  :metaCritic ?metaCritic .
        }
    }
    GROUP BY ?movie ?contentRating ?year ?metaCritic ?rating
}
